controller RunningLights where

input button1 : Bool = False
input button2 : Bool = False

output state1 : Bool = False
output state2 : Bool = False
enum LightNumber = L0 | L1 | L2 | L3 | L4 | L5 | L6 | L7
output light : LightNumber = L0

env_liveness
  True

sys_trans
  !state1 /\ (!button1 \/ light == L0) -> !state1'
  !state1 /\ ( button1 /\ !light == L0) ->  state1'
  state1  /\ !light == L0 ->  state1'
  state1  /\  light == L0 -> !state1'

  !state2 /\ (!button2 \/  light == L7) -> !state2'
  !state2 /\ ( button2 /\ !light == L7) ->  state2'
  state2  /\ !light == L7 ->  state2'
  state2  /\  light == L7 -> !state2'

  !light == L0 /\ !light == L1 -> !light' == L0
  !light == L0 /\ !light == L2 -> !light' == L1
  !light == L1 /\ !light == L3 -> !light' == L2
  !light == L2 /\ !light == L4 -> !light' == L3
  !light == L3 /\ !light == L5 -> !light' == L4
  !light == L4 /\ !light == L6 -> !light' == L5
  !light == L5 /\ !light == L7 -> !light' == L6
  !light == L6 /\ !light == L7 -> !light' == L7

  light == L0 /\ !state1' /\ !state2' -> light' == L0
  light == L7 /\ !state1' /\ !state2' -> light' == L7
  light == L0 /\ !state1  /\ !state2  -> light' == L0
  light == L7 /\ !state1  /\ !state2  -> light' == L7

sys_liveness
  !state1

sys_liveness
  !state2
